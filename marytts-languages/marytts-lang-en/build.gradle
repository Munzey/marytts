plugins {
    id 'groovy'
}

version '5.2-SNAPSHOT'

sourceSets {
    lexicon
    lexiconTest
}

configurations {
    lexiconCMUdict
}

repositories {
    mavenLocal()
    jcenter()
    ivy {
        url 'http://svn.code.sf.net/p/cmusphinx/code/trunk'
        layout 'pattern', {
            artifact '[module]/[artifact]-[revision]'
        }
    }
}

dependencies {
    lexiconCMUdict name: 'cmudict', version: '0.7b', ext: 'txt'
    lexiconCompile localGroovy()
    lexiconCompile group: 'de.dfki.mary', name: 'marytts-common', version: version
    lexiconCompile group: 'de.dfki.mary', name: 'marytts-transcription', version: version
    lexiconTestCompile localGroovy()
    lexiconTestCompile 'org.testng:testng:6.8.13'
    lexiconTestCompile(group: 'de.dfki.mary', name: 'marytts-lang-en', version: '5.1.1') {
        exclude module: 'freetts'
        exclude module: 'freetts-de'
        exclude module: 'freetts-en_us'
    }
}

processLexiconResources {
    from 'lib/modules/en/synthesis', {
        include 'sampa2mrpa_en.map'
    }
    from 'lib/modules/en/us/lexicon', {
        include 'allophones.en_US.xml'
    }
    from configurations.lexiconCMUdict
}

task convertCMUdict(type: JavaExec) {
    description 'Converts CMUdict US lexicon file to plain text, with a SAMPA phoneset instead of ARPAbet'
    dependsOn processLexiconResources
    def (allophoneSetFile, mappingFile, destFile, lexiconFile) = ['allophones.en_US.xml', 'sampa2mrpa_en.map', 'en_US.txt', 'cmudict-0.7b.txt'].collect {
        file("$sourceSets.lexicon.output.resourcesDir/$it")
    }
    inputs.files allophoneSetFile, mappingFile, lexiconFile
    outputs.files destFile
    args inputs.files + outputs.files
    classpath sourceSets.lexicon.runtimeClasspath
    main 'CmuDictConverter'
}

task trainCMUdict(type: JavaExec) {
    description 'Trains LTS rules from converted CMUdict'
    inputs.files "$sourceSets.lexicon.output.resourcesDir/allophones.en_US.xml", convertCMUdict
    outputs.files "$sourceSets.lexicon.output.resourcesDir/en_US.lts", "$sourceSets.lexicon.output.resourcesDir/en_US_lexicon.fst"
    args inputs.files
    classpath sourceSets.lexicon.runtimeClasspath
    main 'marytts.tools.transcription.LTSLexiconPOSBuilder'
}

processResources {
    from trainCMUdict, {
        rename { "marytts/language/en_US/lexicon/$it" }
    }
}

task lexiconTest(type: Test) {
    dependsOn convertCMUdict
    useTestNG()
    testClassesDir = sourceSets.lexiconTest.output.classesDir
    classpath = sourceSets.lexiconTest.runtimeClasspath + sourceSets.lexicon.runtimeClasspath
}