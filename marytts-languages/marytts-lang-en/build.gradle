plugins {
    id 'groovy'
}

sourceSets {
    lexicon
}

dependencies {
    lexiconCompile localGroovy()
}

processLexiconResources {
    from 'lib/modules/en/synthesis', {
        include 'sampa2mrpa_en.map'
    }
    from 'lib/modules/en/us/lexicon/cmu', {
        include '*.scm'
    }
}

task convertLexica {
    description 'Converts lexicon files from CMUdict scheme format to plain text, with a SAMPA phoneset instead of ARPAbet'
    dependsOn lexiconClasses
    def destFile = file("$sourceSets.lexicon.output.resourcesDir/en.txt")
    inputs.files processLexiconResources
    outputs.files destFile
    doLast {
        javaexec {
            classpath sourceSets.lexicon.runtimeClasspath
            main 'CmuDictConverter'
            def mappingFile = file("$sourceSets.lexicon.output.resourcesDir/sampa2mrpa_en.map")
            def lexiconFiles = fileTree(sourceSets.lexicon.output.resourcesDir).include('*.scm').files
            args = [mappingFile, destFile, lexiconFiles].flatten()
        }
    }
}
